# Find the embree library.
#
# This module defines the following variables:
#
# * embree_FOUND
# * embree_INCLUDE_DIRS
# * embree_LIBRARIES
#
# This module defines the following imported targets:
#
# * embree::embree
#
# This module defines the following interfaces:
#
# * embree

find_package(ISPC REQUIRED)
find_package(TBB REQUIRED)

include(embree-config.cmake)

find_path(embree_INCLUDE_DIR NAMES embree3/rtcore.h)
set(embree_INCLUDE_DIRS
    ${embree_INCLUDE_DIR}
    ${TBB_INCLUDE_DIRS})

find_library(embree_LIBRARY NAMES embree3)
set(embree_LIBRARIES
    ${embree_LIBRARY}
    ${TBB_LIBRARIES})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    embree
    REQUIRED_VARS embree_INCLUDE_DIR embree_LIBRARY)
mark_as_advanced(embree_INCLUDE_DIR embree_LIBRARY)

if(embree_FOUND AND NOT TARGET embree::embree)
    add_library(embree::embree UNKNOWN IMPORTED)
    set_target_properties(embree::embree PROPERTIES
        IMPORTED_LOCATION "${embree_LIBRARY}"
        INTERFACE_COMPILE_DEFINITIONS embree_FOUND
        INTERFACE_INCLUDE_DIRECTORIES "${embree_INCLUDE_DIR}"
        INTERFACE_LINK_LIBRARIES "TBB")
endif()
if(embree_FOUND AND NOT TARGET embree)
    add_library(embree INTERFACE)
    target_link_libraries(embree INTERFACE embree::embree)
endif()
